

package compiler.pp1;
/* import sekcija sve sto nam treba za parser klasu */
/* od ovoga nastaju klase parser i action */
import java_cup.runtime.*;
import org.apache.log4j.*;

//ovo se generise pozivom parsera
import compiler.pp1.ast.*;

//kod parsera
parser code {:

	Logger log = Logger.getLogger(getClass());
	boolean errorDetected;


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     //ne mozemo da nsatavimo parsiranje
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

  //prijava greske kad moze da se oporavi od greske
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

  //ako oporavak nije moguc
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

//string builder koristimo za gresku
    public void report_error(String message, Object info) {
        errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left); //left - sadrzi broj linije
        log.error(msg.toString());
    }


:}

init with {:
	errorDetected = false;
:}

//resetovanje dohvatanja svakog tokena od lexera
scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, LBRACE, RBRACE;
terminal CONST,SEMI, COMMA, COLUMN, EQUAL, LBRACK, RBRACK, RECORD;
terminal CLASS, EXTND, LPAREN, RPAREN, DOT;
terminal VOID, NEW;
terminal Integer NUMBER, BOOL;
terminal String IDENT, CHAR;
terminal IF, ELSE, GOTO, DO, WHILE, BREAK, CONT, RETURN, READ, PRINT;
terminal EQU, NEQ, GRT, GRE, LES, LEE, AND, OR;
terminal MUL, DIV, MOD, PLUS, MINUS, INC, DEC, THIS, SUPER;



nonterminal Program, DeclList, MethodDeclList, ConstDecl, VarDecl, ClassDecl, RecordDecl, MethodDecl;
nonterminal ConstDeclList, VarDeclList;
nonterminal InnerVarDeclList, InnerMethodBlock, ConstructorDecl, StatementList;
nonterminal FormParList, Statement, SingleStatement, Statements;
nonterminal Matched, Unmatched, DesignStatement, PrintSize, DsgnOp, AccessDsgn, Assignop, ActPars, ActParsList;
nonterminal Relop, Label, DoStart;
nonterminal Mulop, Addop, IfElseBlock, RelopEq;
nonterminal VarDeclGlobal, VarDeclCheck, Xx, VarDeclListNoC, ClassDeclList, ClassVarDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Cond, CondTerm, CondFact, Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgramName,ConstLit, RecordName, MethodName, Type,Factor, Term, Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ClassName, ExtendsDecl, ConstructorName, RetType, MethodDeclChecker;
nonterminal ConstDeclElem, VarDeclElem, VarDeclElemNoC, LabelDef;

precedence left ELSE;

Program ::= (Program) PROG ProgramName DeclList LBRACE MethodDeclList RBRACE;

ProgramName::= (ProgramName) IDENT:name;

DeclList ::= (DeclListConst) DeclList ConstDecl
            |
            (DeclListVar) DeclList VarDeclGlobal
            |
            (DeclListClass) DeclList ClassDecl
            |
            (DeclListRecord) DeclList RecordDecl
            |
            (DeclListEmpty) /* epsilon */
            ;
ConstDecl ::= (DeclConst) CONST Type:varsType ConstDeclList:decls SEMI
               |
               (ConstDeclError) CONST error SEMI:l
               ;

ConstDeclList ::= (ConstDeclMultiple) ConstDeclList COMMA ConstDeclElem
                |
                (ConstDeclSingle) ConstDeclElem
                ;
ConstDeclElem ::= (ConstDeclElemCorrect) IDENT:varName EQUAL ConstLit:varValue
                |
                (ConstDeclElemError) error
                ;
ConstLit ::=(ConstLitNum) NUMBER:value
            |
            (ConstLitBool) BOOL:value
            |
            (ConstLitChar) CHAR:value
            ;
Type ::= (Type) IDENT:typeName;

VarDeclGlobal ::= (VarDeclGlobalCorrect) Type:varsType VarDeclList:decls SEMI
                |
                (VarDeclGlobalError) error SEMI:l
                ;
VarDecl ::= (VarDecl) Type:varsType VarDeclListNoC:decls SEMI;

ClassVarDecl ::= (VarDeclsCorrect) Type:varsType VarDeclListNoC:decls;

VarDeclListNoC ::= (VarDeclMultipleNoC) VarDeclListNoC COMMA VarDeclElemNoC
                |
                (VarDeclSingleNoC) VarDeclElemNoC
                ;

VarDeclList ::= (VarDeclMultiple) VarDeclList COMMA VarDeclElem
                |
                (VarDeclSingle) VarDeclElem
                ;
VarDeclElemNoC ::= (VarDeclElemArrayNoC) IDENT:varName LBRACK RBRACK
                   |
                   (VarDeclElemSingleNoC) IDENT:varName
                   ;
VarDeclElem ::= (VarDeclElemArray) IDENT:varName LBRACK RBRACK
                |
                (VarDeclElemSingle) IDENT:varName
                |
                (VarDeclError) error
                ;
ClassDecl ::= (ClassDecl) CLASS ClassName ExtendsDecl LBRACE ClassDeclList:vars RBRACE;

ClassName ::= (ClassName)IDENT:name;

ExtendsDecl ::= (ExtendsDeclType) EXTND Type:base
                |
                (ExtendsDeclError) error
                |
                (ExtendsDeclEmpty)/* epsilon */
                ;
InnerVarDeclList ::= (InnerVarDecls) InnerVarDeclList:varList VarDecl:var
                |
                (InnerVarDeclsEmpty) /* epsilon */
                ;
ClassDeclList ::= (ClassVarDecls) ClassVarDecl:var SEMI ClassDeclList:varList
                |
                (ClassVarDeclsError) error SEMI ClassDeclList:varList
                |
                (ClassDeclsMethods) InnerMethodBlock:methods
                |
                (ClassDeclsMethodsError) error InnerMethodBlock:methods
                |
                (ClassDeclsEmpty) /* epsilon */
                ;
InnerMethodBlock ::= (InnerMethodBlockAll) LBRACE ConstructorDecl:constructor MethodDeclList:methods RBRACE
                    |
                    (InnerMethodBlockNoConstr) LBRACE MethodDeclList:methods RBRACE
                    ;
RecordDecl ::= (RecordDecl) RECORD RecordName LBRACE InnerVarDeclList:vars RBRACE;
RecordName ::= (RecordName) IDENT:recordName;
ConstructorDecl ::= (ConstructorDecl) ConstructorName:name LPAREN RPAREN InnerVarDeclList:vars LBRACE StatementList:statements RBRACE;
ConstructorName ::= (ConstructorName) IDENT:name;

MethodDeclList ::= (MethodDecls) MethodDecl:method MethodDeclList
				|
				(MethodDeclsEmpty) /* epsilon */
				;
MethodDecl ::= (MethodDeclPar) MethodDeclChecker:name LPAREN FormParList:params RPAREN InnerVarDeclList:vars LBRACE StatementList:statements RBRACE
                |
                (MethodDeclNoPar) MethodDeclChecker:name LPAREN RPAREN InnerVarDeclList:vars LBRACE StatementList:statements RBRACE
                ;
MethodDeclChecker ::= (MethodDeclChecker) RetType:type MethodName:name;
RetType ::= (RetTypeType) Type:type
        |
        (RetTypeVoid) VOID
        ;
MethodName ::= (MethodName) IDENT:methodName;
FormParList ::= (FormParsMultipleArray) FormParList COMMA Type:type IDENT:parName LBRACK RBRACK
                |
                (FormParsMultiple) FormParList COMMA Type:type IDENT:parName
                |
                (FormParsError) FormParList COMMA error
                |
                (FormParsSingleArray) Type:type IDENT:parName LBRACK RBRACK
                |
                (FormParsSingle) Type:type IDENT:parName
                                |
                (FormParsSingleError) error
                ;
StatementList ::= (StatementsList) StatementList:statements Statement
				|
				(StatementsEmpty) /* epsilon */
				;
Statement ::= (StatementSingle) SingleStatement:statement
            |
            (StatementSingleLabel) LabelDef:label SingleStatement:statement
            |
            (StatementMultiple) Statements
            ;
LabelDef::= (LabelDef) Label:label COLUMN;
Statements ::= (StatementsBlock) LBRACE StatementList RBRACE;

SingleStatement ::= (StmtIfElse) IF LPAREN Cond RPAREN Statement:ifStmt ELSE Statement:elseStmt
            |
            (StmtIfElseError) IF LPAREN error RPAREN Statement:ifStmt ELSE Statement:elseStmt
            |
            (StmtIf) IF LPAREN Cond RPAREN Statement:ifStmt
            |
            (StmtIfError) IF LPAREN error RPAREN Statement:ifStmt
            |
            (StmtDesign) DesignStatement:statement SEMI
            |
            (StmtDoWhile) DoStart Statement WHILE LPAREN Cond RPAREN SEMI
            |
            (StmtBreak) BREAK SEMI
            |
            (StmtContinue) CONT SEMI
            |
            (StmtReturn) RETURN Expr:retValue SEMI
            |
            (StmtReturnVoid) RETURN SEMI
            |
            (StmtRead) READ LPAREN Designator:dst RPAREN SEMI
            |
            (StmtPrintSize) PRINT LPAREN Expr:src COMMA NUMBER:size RPAREN SEMI
            |
            (StmtPrint) PRINT LPAREN Expr:src RPAREN:size SEMI
            |
            (StmtGoto) GOTO Label:dst SEMI
            ;
DoStart ::= (DoStart) DO;
DesignStatement ::= (DesignStmt) Designator DsgnOp:operation;
Designator ::= (DesignatorAccessField) Designator:base DOT IDENT:field
              |
               (DesignatorAccessArray) Designator:base LBRACK Expr:index RBRACK
              |
              (DesignatorFirst) IDENT:dsgnName
              ;

DsgnOp ::= (DsgnOpAssign) Assignop Expr
            |
            (DsgnOpAssignError) Assignop:r error
            |
            (DsgnOpCallPars) LPAREN ActPars:params RPAREN
            |
            (DsgnOpCallEmpty) LPAREN RPAREN
            |
            (DsgnOpInc) INC
            |
            (DsgnOpDec) DEC
            ;
Assignop ::= (Assignop) EQUAL;

ActPars ::= (ActParsMultiple) ActPars:params COMMA Expr:param
                |
                (ActParsSingle) Expr:param
                ;
Cond ::= (CondMultiple) Cond:conds OR CondTerm:cond
        |
        (CondSingle) CondTerm:cond
        ;
CondTerm ::= (CondTermMultiple) CondTerm:conds AND CondFact:cond
            |
            (CondTermSingle) CondFact:cond
            ;
CondFact ::= (CondFactMultiple) Expr:op1 Relop Expr:op2
            |
            (CondFactMultipleEq) Expr:op1 RelopEq Expr:op2
            |
            (CondFactSingle) Expr:operand
            ;
RelopEq ::= (RelopEqu) EQU:val
            | (RelopNeq) NEQ:val
            ;
Relop ::= (RelopGrt) GRT:val
        | (RelopGre) GRE:val
        | (RelopLes) LES:val
        | (RelopLee) LEE:val ;
Addop ::= (AddopPlus) PLUS
        | (AddopMinus) MINUS;
Mulop ::= (MulopMul) MUL
        | (MulopDiv) DIV
        | (MulopMod) MOD;
Expr ::= (ExprMultiple) Expr:operands Addop Term:operand
        |
        (ExprSingle) Term:operand
        |
        (ExprSingleMinus) MINUS Term:operand
        ;
Term ::= (TermMultiple) Term:operands Mulop Factor:operand
        |
        (TermSingle) Factor:operand
        ;
Factor ::= (FactorDsgn) Designator:operand
        |
        (FactorDsgnCall) Designator:fun LPAREN ActPars:params RPAREN
        |
        (FactorNumber) NUMBER:operand
        |
        (FactorChar) CHAR:operand
        |
        (FactorBool) BOOL:operand
        |
        (FactorObject) NEW Type:objType
        |
        (FactorArray) NEW Type:arrType LBRACK Expr:size RBRACK
        |
        (FactorBlock) LPAREN Expr:operand RPAREN
        ;
Label ::= (Label) IDENT:labelName;






